//////////////////////
//Standard Race Pack//
//Version 1.0.4B    //
//////////////////////

block spawn
{
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Human Alliance - [Block] Spawn (START)
	}
	es wcs_setcooldown event_var(userid) 5
	es_getplayerlocation wcs_x1 wcs_y1 wcs_z1 event_var(userid)
	es est_effect 10 #a 0 sprites/cbbl_smoke.vmt server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1) 40 60 1.5 10 10 0 255 255 255 255 1
	es_xmath wcs_z1 + 12
	es est_effect 10 #a 0 sprites/cbbl_smoke.vmt server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1) 80 100 1.5 10 10 0 255 255 255 255 1
	es_xmath wcs_z1 + 12
	es est_effect 10 #a 0 sprites/cbbl_smoke.vmt server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1) 60 80 1.5 10 10 0 255 255 255 255 2
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Human Alliance - [Block] Spawn (END)
	}
}

block invisibility
{
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Human Alliance - [Block] Skill: 1 (START)
	}
	es_delayed .5 wcs_color event_var(userid) 255 255 255 server_var(wcs_invis) 0
 	es_xmath wcs_invis / -2.55
	es_xmath wcs_invis + 100
    es wcs_decimal wcs_invis server_var(wcs_invis)
    es_tell event_var(userid) #multi #green[WCS]#lightgreen Invisibility - You spawned with server_var(wcs_invis)% invisibility
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Human Alliance - [Block] Skill: 1 (END)
	}
}

block devotion
{
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Human Alliance - [Block] Skill: 2 (START)
	}
	es wcs_setfx health event_var(userid) + server_var(wcs_addhealth) 0
	es_tell event_var(userid) #multi #green[WCS]#lightgreen Devotion aura - Increased your health by server_var(wcs_addhealth)
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Human Alliance - [Block] Skill: 2 (END)
	}
}

block bash
{
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Human Alliance - [Block] Skill: 3 (START)
	}
	if (server_var(wcs_dice3) <= server_var(wcs_chance)) do
	{
		if (event_var(es_userdead) = 0) do
		{
			if (event_var(es_attackerdead) = 0) do
			{
				es wcsgroup get is_frozen wcs_tmp4 event_var(userid)
				if (server_var(wcs_tmp4) == 0) do
				{
					es wcsgroup set is_frozen event_var(userid) 1
					es_delayed 1 wcsgroup set is_frozen event_var(userid) 0
					es wcs_setfx freeze event_var(userid) = 1 1
					es wcs_centertell event_var(attacker) Bash immobilized event_var(es_username) for 1 second!
					es wcs_centertell event_var(userid) event_var(es_attackername)'s Bash immobilized you for 1 second!
					es_getplayerlocation wcs_x1 wcs_y1 wcs_z1 event_var(attacker)
					es_getplayerlocation wcs_x2 wcs_y2 wcs_z2 event_var(userid)
					es_xmath wcs_z1 + 20
					es_xmath wcs_z2 + 20
					es est_effect 3 #a 0 sprites/cbbl_smoke.vmt server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1) server_var(wcs_x2) server_var(wcs_y2) server_var(wcs_z2) 0.5 10 10 255 255 255 255
					es_xmath wcs_z2 + 5
					es est_effect 10 #a 0 sprites/cbbl_smoke.vmt server_var(wcs_x2) server_var(wcs_y2) server_var(wcs_z2) 20 45 0.5 25 10 0 255 255 255 255 0
				}
			}
		}
	}
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Human Alliance - [Block] Skill: 3 (END)
	}
}

block teleport
{
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Human Alliance - [Block] Skill: 4 (START)
	}
	es_xset wcs_ok 1
	es playerget viewcoord server_var(wcs_userid) wcs_x1 wcs_y1 wcs_z1
	es_getplayerlocation wcs_x2 wcs_y2 wcs_z2 server_var(wcs_userid)
	es_xcopy wcs_x wcs_x1
	es_xcopy wcs_y wcs_y1
	es_xcopy wcs_z wcs_z1
	es_math wcs_x - server_var(wcs_x2)
	es_math wcs_y - server_var(wcs_y2)
	es_math wcs_z - server_var(wcs_z2)
	if (server_var(wcs_x) < 0) then es_xmath wcs_x * -1
	if (server_var(wcs_y) < 0) then es_xmath wcs_y * -1
	if (server_var(wcs_z) < 0) then es_xmath wcs_z * -1
	ifx parse("(wcs_range < wcs_x) or (wcs_range < wcs_y) or (wcs_range < wcs_z)") do
	{
		es_xset wcs_ok 0
	}
	ifx true(wcs_ok) do
	{
		es_createvectorstring vector1 server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1)
		es wcs_pushto server_var(wcs_userid) server_var(vector1) 1.8
		es_playsound server_var(wcs_userid) wcs/teleport.mp3 1
	}
	es_xelse do
	{
		if (server_var(wcs_range) < server_var(wcs_x)) do
		{
			if (server_var(wcs_x2) >= 0) do
			{
				if (server_var(wcs_x1) >= 0) do
				{
					if (server_var(wcs_x2) <= server_var(wcs_x1)) then es_xmath wcs_x2 + server_var(wcs_range)
					if (server_var(wcs_x2) > server_var(wcs_x1)) then es_xmath wcs_x2 - server_var(wcs_range)
				}
				es_xelse do
				{
					es_math wcs_x2 - server_var(wcs_range)
				}
			}
			es_xelse do
			{
				if (server_var(wcs_x1) < 0) do
				{
					if (server_var(wcs_x2) <= server_var(wcs_x1)) then es_xmath wcs_x2 + server_var(wcs_range)
					if (server_var(wcs_x2) > server_var(wcs_x1)) then es_xmath wcs_x2 - server_var(wcs_range)
				}	
				es_xelse do
				{
					es_math wcs_x2 + server_var(wcs_range)
				}
			}
			es_xcopy wcs_x1 wcs_x2
		}
		if (server_var(wcs_range) < server_var(wcs_y)) do
		{
			if (server_var(wcs_y2) >= 0) do
			{
				if (server_var(wcs_y1) >= 0) do
				{
					if (server_var(wcs_y2) <= server_var(wcs_y1)) then es_xmath wcs_y2 + server_var(wcs_range)
					if (server_var(wcs_y2) > server_var(wcs_y1)) then es_xmath wcs_y2 - server_var(wcs_range)
				}
				es_xelse do
				{
					es_math wcs_y2 - server_var(wcs_range)
				}
			}
			es_xelse do
			{
				if (server_var(wcs_y1) < 0) do
				{
					if (server_var(wcs_y2) <= server_var(wcs_y1)) then es_xmath wcs_y2 + server_var(wcs_range)
					if (server_var(wcs_y2) > server_var(wcs_y1)) then es_xmath wcs_y2 - server_var(wcs_range)
				}
				es_xelse do
				{
					es_math wcs_y2 + server_var(wcs_range)
				}
			}
			es_xcopy wcs_y1 wcs_y2
		}
		if (server_var(wcs_range) < server_var(wcs_z)) do
		{
			if (server_var(wcs_z2) >= 0) do
			{
				if (server_var(wcs_z1) >= 0) do
				{
					if (server_var(wcs_z2) <= server_var(wcs_z1)) then es_xmath wcs_z2 + server_var(wcs_range)
					if (server_var(wcs_z2) > server_var(wcs_z1)) then es_xmath wcs_z2 - server_var(wcs_range)
				}
				es_xelse do
				{
					es_math wcs_z2 - server_var(wcs_range)
				}
			}
			es_xelse do
			{
				if (server_var(wcs_z1) < 0) do
				{
					if (server_var(wcs_z2) <= server_var(wcs_z1)) then es_xmath wcs_z2 + server_var(wcs_range)
					if (server_var(wcs_z2) > server_var(wcs_z1)) then es_xmath wcs_z2 - server_var(wcs_range)
				}
				es_xelse do
				{
					es_math wcs_z2 + server_var(wcs_range)
				}
			}
			es_xcopy wcs_z1 wcs_z2
		}
		es_createvectorstring vector1 server_var(wcs_x1) server_var(wcs_y1) server_var(wcs_z1)
		es wcs_pushto server_var(wcs_userid) server_var(vector1) 1.8
		es_playsound server_var(wcs_userid) wcs/teleport.mp3 1
	}
	ifx true(wcs_race_debugging) do
	{
		wcs_log [Race]: Human Alliance - [Block] Skill: 4 (END)
	}
}